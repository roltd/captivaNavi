Sub Copy( path, newPath )
	@Log("copy: " & path & " to " & newPath)
	If ( FileExists( path ) )
		If ( not FileExists( newPath ) )
			Copy( path , newPath, FALSE )
			@Log("copy done")
			Return(TRUE)
		else 
			@Log("file exists: " & newPath)
			Return(FALSE)
		EndIf
	else 
		@Log("file not found: " & path)
		Return(FALSE)
	EndIf
EndSub

Sub Rename( path, newPath )
	@Log("rename: " & path & " to " & newPath)
	If ( not FileExists( newPath ) )
		if( FileExists( path ))
			Rename( path , newPath, FALSE )
			@Log("rename done")
			Return(TRUE)
		else
			@Log("file not found: " & path )
		EndIf
	else 
		@Log("file exists: " & newPath)
		Return(FALSE)
	EndIf
EndSub

Sub Delete( path)
	@Log("delete: " & path)
	If ( FileExists( path ) )
		Delete( path )
		@Log("delete done")
		Return(TRUE)
	else 
		@Log("file not found: " & path)
		Return(FALSE)
	EndIf
EndSub

Sub Run( path )
	@Log("run: " & path)
	If( NOT FileExists(path) )
		@Log("file not found: " & path)
		ExitSub(FALSE)
	EndIf

	If( ProcExists( @GetProcessName(path)) )
		@Log("process found: " & path)
		ExitSub(FALSE)
	EndIf

	Run( path ) 
	@Log("started")
	Return(TRUE)
EndSub

Sub CreateOrigPath( path )
	Return( Replace( path , ".", "_orig." ))
EndSub


Sub GetPath( path )
	Return( SubStr( path , 1, ReverseFind(path, '\')))
EndSub

Sub Log ( text )
	If( "true" eq ToLower(logEnabled) )
		WriteFile( logFileName, FormatTime( "H:i:s - " ) & text & "^NL^", True)
		CloseFile( logFileName )
	EndIf
EndSub

Sub GetProcessName ( text )
	index = ReverseFind( text, "\" )
	If ( index > 0 )
		Return( ToLower(SubStr( text, index + 1)) )
	Else
		Return( ToLower(text) )
	EndIf
EndSub

Sub CloseComAgent()
	title = IniRead( "navi.ini", "title", "comAgent" )
	if(WndExists(title))
		Close (title)
		@Log("comagent window closed")
	else 
		@Log("comagent window not found")
	Endif
	Sleep(100)
EndSub

Sub InitNaviArray()
  ForEach section in iniSections("navi.ini")
    If( Find(section, "navi") > 0)
      index = 0 + CharAt( section, 5)
      naviName[index] = IniRead( "navi.ini", section, "name" )
      naviPath[index] = IniRead( "navi.ini", section, "path" )
    EndIf
  EndForEach
EndSub

Sub FindProcWindow( proc )
	windows = FindWindows( "")
	@Log( "Window " & Join(windows , " ") )
	For i = 1 to ElementCount(windows)
		@Log(windows [i] & " - " & WindowProcess( windows [i])))
		If( ToLower(@GetProcessName(proc)) eq ToLower(WindowProcess( windows [i] )) )
			@Log("found " & windows [i] & " - " & pro)
			ExitSub(windows [i])
		EndIf
	Next
	@Log("not found window for " & proc)
	Return(0)
EndSub

Sub RunOrActivate( proc )
	If( ProcExists( @GetProcessName( proc ) ) )
		procWindow = @FindProcWindow( @GetProcessName(proc) )
		If( procWindow ne 0 ) 
			@Log("found window " & procWindow & "for " & proc)
			@ActivateWindow( procWindow )
			ExitSub()
		EndIf
	EndIf
	Run( proc )
EndSub

Sub ActivateWindow( title )
	@Log("ActivateWindow " & title)
	WaitFor(title, 1)
	Repeat(5)
		Show(title)
		Sleep(200)
		active = ActiveWindow()
		If(title ne active)
			Minimize(active)
		else
			ExitSub()
		EndIf
	EndRepeat
EndSub

Sub SearchCustomNavi()
#@Log( "!naviPath count: " & MaxIndex(naviPath) )
  For j = 2 to MaxIndex(naviPath)
    @Log( "search for " & j & " - " & naviPath[j] )
    procWindow = @FindProcWindow( @GetProcessName( naviPath[j] ) )
    If( procWindow ne 0 ) 
      @Log( "custom navi found " & procWindow )
      ExitSub(procWindow)
    EndIf
  Next
  Return(0)
EndSub

Sub GotoOriginMenu( originWindow )
    menuX = IniRead( "navi.ini", "navi1", "menu.x" )
    menuY = IniRead( "navi.ini", "navi1", "menu.y" )
    MouseClick( originWindow, menuX, menuY )
EndSub
